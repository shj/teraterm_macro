;
; パスワード登録マクロ
; CSV形式のファイルからパスワードを読み込んで登録する
; 既存パスワードは更新される
;
; 作成日: 2015/8/5
; 作成者: shj@netwiz.jp
;

; setpassword再試行回数
; 連続してsetpasswordするとエラーとなる場合があるので、再試行する
; この回数を試行してもエラーとなる場合、setpassword失敗とみなす
ntrysetpassword = 10000

; 入力ファイルのセパレータ
separator = ','

ident = ''
newpasswd = ''

getdir ttmacrodir
setdir ttmacrodir
include 'conf\default.ttl'

; 設定したいパスワード一覧のファイルを選択
filenamebox '取り込むパスワードが記述されたファイルを選択' 0
if result == 0 then
  end
endif
inputfile = inputstr

fileopen fh inputfile 0
if result == -1 then
  sprintf2 errmsg '%s を開くことができません\n既にファイルを開いていないか確認してください'
  strspecial errmsg
  messagebox errmsg 'エラー'
  end
endif

yesnobox '既に存在しているパスワードは上書きされます！よろしいですか？' '確認'
if result == 0 then
  end
endif

; カウンタ
overwrite_pass = 0
new_pass = 0
npass = 0
nskip = 0
nfailded = 0
nsuccessful = 0

while 1
  filereadln fh buf
  if result then
    break
  endif

  npass = npass + 1

  strsplit buf separator
  ident = groupmatchstr1
  newpasswd = groupmatchstr2

  strlen ident
  if result == 0 then
    nskip = nskip + 1
    continue
  endif
  strlen newpasswd
  if result == 0 then
    nskip = nskip + 1
    continue
  endif

  for i 1 ntrysetpassword
    setpassword passwdfile ident newpasswd
    if result != 0 then
      failflag = 0
      break
    else
      failflag = 1
      endif
  next
  if failflag then
    nfailded = nfailded + 1
  else
    nsuccessful = nsuccessful + 1
  endif
endwhile

sprintf2 finmsg 'パスワードの更新が完了しました\n\n読み込みフィールド数： %d\nフォーマットエラーにより無視した数：%d\n\n登録成功数： %d\n登録失敗数： %d' npass nskip nsuccessful nfailded
strspecial finmsg
messagebox finmsg 'パスワード更新完了'

end
