;
; 踏み台サーバ使用版
; このマクロは踏み台サーバにログインしている状態で呼び出される事を想定しています
;
; BIG-IP UCS取得マクロ
;

getdir ttmldir
setdir ttmldir
include 'conf\default.ttl'
include 'conf\shared.ttl'

; CSVファイルから接続先情報を読み込む
csv_file = 'conf\bigip.csv'

fileopen fh_csv_file csv_file 0
if fh_csv_file == -1 then
    messagebox 'csvファイルを開けません' 'エラー'
    end
endif

inputbox '指定文字列' 'befor/after/etc..' 'before'
mystr = inputstr

gettime timestr "%Y%m%d_%H%M%S"

while 1
    filereadln fh_csv_file csvbuf
    if result then
        break
    endif

    ; コメントアウトは除外
    strmatch csvbuf '^#'
    if result != 0 then
        continue
    endif
    
    strsplit csvbuf separator 9
    call csvtrim

    target_ip = groupmatchstr1
    target_hostname = groupmatchstr2
    target_loginuser = groupmatchstr4

    ; ログインパスワード取得
    ispassword  passwdfile target_loginuser
    if result = 0 then
        messagebox 'パスワードが未設定です' 'エラー'
        end
    endif
    getpassword passwdfile target_loginuser login_password

    sprintf2 conbuf '%s:22 /ssh /2 /auth=challenge /user=root /passwd=%s' target_ip login_password
    connect conbuf
    wait 'root@'
    
    ; UCS作成
    sprintf2 ucs_no_privatekey '%s_%s_no-private-key_%s' target_hostname mystr timestr 
    sprintf2 ucs_privatekey    '%s_%s_private-key_%s'    target_hostname mystr timestr 
    login_username = 'root'
    sprintf2 query_ucs_no_privatekey 'tmsh save sys ucs %s no-private-key' ucs_no_privatekey
    sprintf2 query_ucs_privatekey    'tmsh save sys ucs %s'                ucs_privatekey

    sendln query_ucs_no_privatekey
    waitregex 'is saved.'
    pause 1

    sendln query_ucs_privatekey
    waitregex 'is saved.'
    pause 1

    ; BIG-IPでのUCSファイルフルパス
    sprintf2 full_ucs_no_privatekey '/var/local/ucs/%s.ucs' ucs_no_privatekey
    sprintf2 full_ucs_privatekey    '/var/local/ucs/%s.ucs' ucs_privatekey

    ; 端末へダウンロードする
    getenv 'HOMEPATH' homepath
    sprintf2 dldir '%s\Downloads' homepath
    sprintf2 dlname1 '%s\%s.ucs'   dldir ucs_no_privatekey
    sprintf2 dlname2 '%s\%s.ucs'   dldir ucs_privatekey

    scprecv full_ucs_no_privatekey dlname1
    scprecv full_ucs_privatekey    dlname2

    yesnobox 'done' 'done'
    sendln 'exit'
    unlink
endwhile

end

; 文字列前後の「"」「'」を削除する
; 文字列途中の「"」「'」は削除されない
:csvtrim
    s = "'"
    w = '"'
    for i 1 9
        sprintf 'strtrim groupmatchstr%d %s' i  '"'"'
        execcmnd inputstr
        sprintf 'strtrim groupmatchstr%d %s' i  "'"'"
        execcmnd inputstr
    next

    return
end
