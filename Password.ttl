;
; パスワード設定・変更マクロ
;
; 《注意》
; ユーザー名（識別子）はログインするためのユーザー名ではありません
; 後でパスワードファイルから取得する際の問い合わせ文字列です
; そのためホスト名など他と重複しない値を指定する必要があります
;

getdir ttmldir
include 'conf\default.ttl'

getver version '4.75'
if result < 0 then
    messagebox 'このマクロはTeraTerm 4.75以降でのみ動作します' 'TeraTermバージョンエラー'
    end
endif

passname = ''
passwd = ''

:xsetpassname
yesnobox 'パスワード設定を行いますか？' 'パスワード設定'
if result == 0 then
    end
endif

while 1
    inputbox 'ユーザー名（識別子）' 'ユーザー名（識別子）'
    strlen inputstr
    if result > 0 then
        passname = inputstr
        break
    endif
endwhile

while 1
    passwordbox  'パスワード' 'パスワード'
    strlen inputstr
    if result > 0 then
        passwd = inputstr
        break
    endif
endwhile

;
; 《注意》
; 確認メッセージでパスワードを表示しているので
; パスワードを表示したくない場合は、ここの処理を削除してください 
;
sprintf2 tmp 'これでいいですか？\n\nユーザー名（識別子） %s\nパスワード %s' passname passwd
yesnobox tmp '最終確認' 1
if result == 0 then
    goto xsetpassname
endif

ispassword passwdfile passname
if result then
    sprintf2 tmp '設定したユーザー名（識別子）[%s] は既に設定されています。\n上書きしますか？' passname
    yesnobox tmp '上書き確認' 1
    if result == 0 then
        goto xsetpassname
    endif
endif

setpassword passwdfile passname passwd
if result then
    messagebox 'パスワードの設定に成功しました' '設定完了'
else
    messagebox 'パスワードの設定に失敗しました' 'エラー'
endif